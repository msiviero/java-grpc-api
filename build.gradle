plugins {
    id 'java'
    id 'application'
    id 'com.google.protobuf' version '0.8.8'
    id "org.flywaydb.flyway" version "6.4.4"
}

repositories {
    jcenter()
}

dependencies {
    def grpcVersion = '1.30.0'
    def autoValueVersion = '1.7.3'
    def daggerVersion = '2.27'
    def floggerVersion = '0.5.1'
    def jwtVersion = '0.11.2'

    implementation "io.grpc:grpc-netty-shaded:$grpcVersion"
    implementation "io.grpc:grpc-protobuf:$grpcVersion"
    implementation "io.grpc:grpc-stub:$grpcVersion"

    implementation "com.google.auto.value:auto-value-annotations:$autoValueVersion"
    implementation 'com.google.guava:guava:28.2-jre'
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.flogger:flogger:$floggerVersion"
    implementation "com.google.flogger:flogger-system-backend:$floggerVersion"
    implementation 'com.google.code.gson:gson:2.8.6'

    implementation 'mysql:mysql-connector-java:8.0.20'
    implementation 'org.springframework:spring-jdbc:5.2.7.RELEASE'
    implementation 'com.zaxxer:HikariCP:3.4.5'
    implementation "io.jsonwebtoken:jjwt-jackson:$jwtVersion"
    implementation "org.slf4j:slf4j-simple:1.7.30"


    testImplementation 'junit:junit:4.13'
    testImplementation "com.google.truth:truth:1.0.1"
    testImplementation "org.mockito:mockito-core:3.3.3"
    testImplementation "io.grpc:grpc-testing:$grpcVersion"

    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
    annotationProcessor "com.google.auto.value:auto-value:$autoValueVersion"

    runtime "io.jsonwebtoken:jjwt-impl:$jwtVersion"
    runtime "io.jsonwebtoken:jjwt-jackson:$jwtVersion"
}

application {
    mainClassName = 'dev.msiviero.App'
}

flyway {
    url = 'jdbc:mysql://127.0.0.1/app'
    user = 'api'
    password = 'password'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.11.0"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.29.0'
        }
    }
    generateProtoTasks {
        //noinspection GroovyAssignabilityCheck
        all()*.plugins {
            grpc {}
        }
    }
}

jar {
    manifest {
        attributes(
                'Main-Class': 'dev.msiviero.App'
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}


sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

